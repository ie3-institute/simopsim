import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer

plugins {
  id 'java' // java support
  id 'maven-publish' // publish to a maven repo (local or mvn central, has to be defined)
  id "com.github.johnrengelman.shadow" version "8.1.1"
  id 'com.diffplug.spotless' version '6.25.0'// code format
  id "de.undercouch.download" version "5.6.0" // downloads plugin
}

ext {
  //version (changing these should be considered thoroughly!)
  javaVersion = JavaVersion.VERSION_17

  scalaVersion = '3'
  tscfgVersion = '1.1.3'

  scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator // location of script plugins
}

group = 'com.github.ie3-institute'
description = 'simopsim'

java {
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
}

apply from: scriptsLocation + 'spotless.gradle'

configurations {
  scalaCompilerPlugin
}

repositories {
  mavenCentral() // searches in Sonatype's central repository
  maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' } // sonatype snapshot repo
  maven { url 'https://packages.confluent.io/maven' } // confluent repo (kafka)
}

dependencies {
  implementation 'com.opencsv:opencsv:5.5.2'
  implementation 'tech.units:indriya:2.2.3' // quantities

  // ieÂ³ internal repository
  implementation('com.github.ie3-institute:PowerSystemUtils:3.1.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'com.github.ie3-institute'
  }
  implementation('com.github.ie3-institute:PowerSystemDataModel:8.0.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'com.github.ie3-institute'
  }
  implementation('com.github.ie3-institute:simonaAPI:0.9.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'edu.ie3'
  }

  // opsim dependencies
  shadow('javax.xml.bind:jaxb-api:2.3.1')
  shadow('org.glassfish.jaxb:jaxb-runtime:2.3.1')

  implementation files('libs/iee-opsim-datamodel-3.3.8.jar')
  implementation files('libs/iee-opsim-client-2.2.23-SNAPSHOT-jar-with-dependencies.jar')

  /* config */
  implementation 'com.typesafe:config:1.4.3'

  /* logging */
  implementation "com.typesafe.scala-logging:scala-logging_${scalaVersion}:3.9.5" // pekko scala logging
  implementation "ch.qos.logback:logback-classic:1.4.14"
}

shadowJar {
  mergeServiceFiles()

  excludes.remove("module-info.class")

  transform(AppendingTransformer) {
    resource = 'reference.conf'
  }
  zip64 = true
  archiveBaseName.set('simopsim')
}
